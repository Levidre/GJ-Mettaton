//---------------------------Anchors and OnTranslate--------------------------

//This .dic file is for setting up anchor words, which are links to set conversations attached to certain words automatically. It also deals with OnTranslate, which is a fairly unique function. All text goes through OnTranslate first before it displays on your ghost, which is how it knows to turn certain words into links. Before you try it yourself, I've already tried adding automatic \w8 pauses after periods and that totally breaks everything.
//Why should I care about anchor words, you ask? Or what can I use them for? Well, you can set up anchor words to easy explanations for things your ghost might talk about that might confuse your user. People they know, places they've been, pop culture references the user might not get, information about you the creator or the world the characters from, items in an item menu, basically any number of things. Anchor words can be a handy repository of information! Or just an easy way to set up some cheap jokes. The power is yours!

//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/walkthrough.html) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!

//This file is very short, so don't worry! And if you aren't planning on using anchor words, you just have to delete a few things and you'll be done!


//--OnTranslate

//As I said above, OnTranslate filters through all the text your ghost displays and does whatever you specify to it. This is a powerful function that can be used in a lot of creative ways I bet, but I wouldn't mess with it too much since you can pretty easily break stuff.
//If you're not interested in any anchor words, delete all the lines that start with _talk = REPLACE under OnTranslate.

OnTranslate
	{
	_talk = reference0
 	_talk = REPLACE(_talk, "Anchor 1", "\_a[anchor1]Anchor 1\_a")
 	_talk = REPLACE(_talk, "Anchor 2", "\_a[anchor2]Anchor 2\_a")
 	_talk = REPLACE(_talk, "Anchor 3", "\_a[anchor3]Anchor 3\_a")
		{
		"%(_talk)"
		}
	}
	
	//Some brief explanation about what's going on in those REPLACE lines! Basically, the function looks through the script and looks for a defined word or set of words. In this case, it's "Anchor 1". It then replaces "Anchor 1" with the second set of text, "\_a[anchor1]Anchor 1\_a". \_a if you recall is the dialogue code for linking something, so this is linking the words Anchor 1 to something called anchor1 set up in the next function, OnAnchorSelect.
	//This is being used to change a word into a link, but you could conceivably use this to replace any text with any other kind of text if you wanted. I tried to replace . with .\w8  as I mentioned above which did not turn out well, haha, but there are probably other ways this could be used successfully.

	
//--OnAnchorSelect
//OnAnchorSelect runs whenever someone clicks one of the links as specified above, or a link that begins with http://.

OnAnchorSelect
{
	if "http://" _in_ reference0 //checking to see if the link is a web address
	{
	"\j[%(reference0)]\e" // \j is a tag that will open a specified url in the user's browser of choice. This basically just tells the ghost to open the webpage stored in reference0, which would be the address that the user just clicked. I didn't get too much into \j in the walkthrough page because I have literally never used it except here.
	}
	else //if it's NOT a web address
	{
	case reference0 //case is a unique thing I've only really seen here in anchor.dic, and as far as I can tell it sets up reference0 for evaluation to see if matches any of the below criteria, which are all tagged with when. I wouldn't mess with much of this stuff unless you have a better grasp of this kind of thing than I do, haha. You can add as many things here as you want to set up above as long as you follow the basic format.
		{
			//It's if the user want to change the playing mode. This will reset the playlists already read except for randomly because if the user doesn't want to quit the random mode, the player will continue to play normally.
			when "onemu" //if the link is this value
			{
				muplayed = IARRAY
				--
				"\![leave,passivemode]"
				--
				"* ONE SONG WOULD BE PLAYED."
				--
				playmode = reference0
				--
				"\e"
				--
				break
			}
			when "onemuloop" //if the link is this value
			{
				muplayed = IARRAY
				--
				"\![leave,passivemode]"
				--
				"* THE SAME SONG WOULD BE PLAYED IN LOOP."
				--
				playmode = reference0
				--
				"\e"
				--
				break
			}
			when "oneplaylist" //if the link is this value
			{
				muplayed = IARRAY
				--
				"\![leave,passivemode]"
				--
				"* THE WHOLE PLAYLIST WOULD BE PLAYED."
				--
				playmode = reference0
				--
				"\e"
				--
				break
			}
			when "playlistloop"
			{
				muplayed = IARRAY
				--
				"\![leave,passivemode]"
				--
				"* THE PLAYLIST WOULD BE PLAYED IN LOOP."
				--
				playmode = reference0
				--
				"\e"
				--
				break
			}
			when "random"
			{
				"\![leave,passivemode]"
				--
				"* THE SONGS WOULD BE PLAYED RANDOMLY."
				--
				playmode = reference0
				--
				"\e"
				--
				break
			}
			when "prevplaylist"
            {   
				"\![leave,passivemode]\![raise,OnChoiceSelect,loadprevplaylist]"
				--
				break
            }
		}
	foreach totalsongs ; song 
	{
		//If the user selected a music
		name = "%((SPLITPATH(song))[2])"
		name = REPLACE(name,"[","t")
		name = REPLACE(name,"]","t")
		name = REPLACE(name,"(","t")
		name = REPLACE(name,")","t")
		name = REPLACE(name,"{","t")
		name = REPLACE(name,"}","t")
		name = REPLACE(name,"'","t")
		name = REPLACE(name,",","t")
		name = REPLACE(name,"\","t")
		name = REPLACE(name,"!","t")
		if reference0 == "%(name)"
		{
			"\![leave,passivemode]"
			//This music would be removed or played depending on what he wanted.
			--
			if suppr != 1
			{
				isplayed = 0
				songplayed = song
				Select.playmu
			}
			else
			{
				suppr = 0
				songremoved = song
				Removesong
			}
		}
	}
	--
	if reference0 == TOINT(reference0)
	{
		"\![leave,passivemode]"
		//This music would be removed or played depending on what he wanted.
		--
		if suppr != 1
		{
			isplayed = 0
			songplayed = TOINT(reference0)
			if displaytest == 1
			{
				"* I want %(songplayed)"
			}
			Select.playmu
		}
		else
		{
			"* YOU CANNOT REMOVE A SONG FROM A CD.\e"
		}
	}
	--
	foreach playlistlist ; playlist
	{
		//The same things than for the musics but for the playlists.
		//The change is that instead of playing a music begin the processus for loading the playlist, so playlistload change to 1. This will change the use of the other functions called.
		if reference0 == "%(playlist)"
		{
			"\![leave,passivemode]"
			--
			if suppr != 1
			{
				if displaytest == 1
				{
					"* I load a playlist so I will save first the one I already have.\n"
				}
				--
				//The fist step in loading a playlist is saving the current one.
				if currentplaylist != "newone" || ARRAYSIZE(songlist) < 1
				{
					currentplaylist = "%(playlist)"
					playlistloaded = "%(playlist)"
					playlistload = 1
					Select.cleanplaylist
				}
				else
				{
					currentplaylist = "%(playlist)"
					playlistname = "tempplaylist"
					playlistloaded = "%(playlist)"
					playlistload = 1
					Select.Saveplaylist
				}
			}
			else
			{
				//If the user want to remove a playlist. I have to do a function to erase the file too.
				suppr = 0
				playlistremoved = playlist
				Removeplaylist
			}
		}

	}
}
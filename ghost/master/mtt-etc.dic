//---------------------------Miscellaneous Functions--------------------------


//--OnFileDrop2


OnFileDrop2
{
	ref0 = reference0
	_droppedfilepath = SPLITPATH(reference0)
	_filepath = _droppedfilepath[1]
	_filename = _droppedfilepath[2]
	_fileext = TOLOWER(_droppedfilepath[3])
	addaplaylist = 0
	case _fileext
	{
		when ".jpg", ".jpeg", ".bmp" 
		{
			Wallpaperfile = reference0
			
			"* THE FILE YOU DROPPED %(_filename)%(_fileext).\w8HOW WOULD YOU LIKE ME TO SET THIS PICTURE AS YOUR WALLPAPER?\n\n[half]/
			\q[Centered,SetWallpaper_0]\n/
			\q[Tiled,SetWallpaper_1]\n/
			\q[Stretched,SetWallpaper_2]\n\n[half]/
			\q[No thanks,SetWallpaper_Cancel]\e"
		}
		when ".mid", ".wav", ".mp3", ".wma", ".au", ".ogg"//Other file formats you might want to give your ghost a specific response to.
		{
			"* A NEW SONG? \w7THAT'S NICE\n"
			"* WONDERFUL! \w8SOMETHING NEW TO PLAY!\n"
			--
			reppath = reference0
			--
			addfiledropped
		}
		when ".m3u",".pls"
		{
			"* WHAT A BUNCH OF NEW SONGS.\w7 OF COURSE I ADD THEM TO MY PLAYLIST.\n"
			"* A WONDERFUL COLLECTION OF NEW SONGS TO ADD!\n"
			--
			AddPlaylistDropped
		}
		others 
		{
			if (FATTRIB (ref0))[2] == 1 {
				reppath =0
				//Annuaire
				folderpath =reference0
				//Annuaire
				repdropped
				//Ajustement du chemin du repertoire
				}
			//Traitez-le comme un fichier s'il ne s'agit pas d'un repertoire
			else 
			{
			
				"\0* THIS IS %(_filename)%(_fileext).\w7 I DON'T KNOW WHAT TO DO WITH IT.\e"
			}
		}
	}
}

//This set of options sets the wallpaper up. You can replace the dialogue, but make sure to leave the \![set,wallpaper, etc.] tag at the beginning.

Select.SetWallpaper_0
{
	"\![set,wallpaper,%(Wallpaperfile),center]\0* THE IMAGE IS CENTERED.\e"
}
Select.SetWallpaper_1
{
	"\![set,wallpaper,%(Wallpaperfile),tile]\0* THE IMAGE IS TITLED.\e"
}
Select.SetWallpaper_2
{
	"\![set,wallpaper,%(Wallpaperfile),stretch]\0* THE IMAGE IS STRETCHED.\e"
}
Select.SetWallpaper_Cancel
{
	"\0* YOU DON'T WANT TO SET THE FILE AS YOUR DESKTOP BACKGROUND.\w7OKAY.\e"
}


//Les installations à effectuer pour les fichiers ajoutés pour le lecteur mp3/The functions for the musics and playlists drop
//This add the musics of a playlist to our playlist and is the next step of loading a playlist. This only works if you already have a music in the current playlist. If not, only the last music will be added.
AddPlaylistDropped
{
	_f=FOPEN (ref0,"r")
	_txt=0
	_i=0
	AllFilepath = IARRAY
	--
	if displaytest == 1
	{
		"\b2* I bring all the musics of the playlist.\n"
	}
	--
	while _txt != -1 && _i<5000 {
		_i++
		_txt = FREAD (ref0)
		_d = _i-2
		//先頭にFile*=がついてたらRemove_Song
		if (STRSTR(_txt,"File%(_d)=",0))==0 
		{
			_txt = REPLACE(_txt, "File%(_d)=", "")
		}
		//先頭に#EXTM3Uや#EXTINFや[playlist]がついてなかったら○AllFilepathに追加
		//Si # EXTM 3 U, # EXT NF ou [playlist] n'est pas attache au debut ○ ajoute a AllFilepath
		if (STRSTR(_txt,"[playlist]",0))!=0 && (STRSTR(_txt,"#EXTM3U:",0))!=0 && (STRSTR(_txt,"#EXTINF:",0))!=0 && _txt!=-1 
		{
			AllFilepath = (AllFilepath,_txt)
			--
			//"Before, I want %(AllFilepath[0]) and %(AllFilepath[1])."
			--
		}
	}
	--
	"* I ADDED THE PLAYLIST.\n"
	--
	addaplaylist = 1
	FCLOSE (ref0)
	if (ARRAYSIZE(AllFilepath)) >=1 {
	addfiledropped
	// Traitement du fichier de depot
	}
}

//If the user added a folder.
repdropped
{
	//Enregistrer tous les fichiers contenus dans le repertoire
	//It add all the path of the files inside the folder to a list (for advanced users, it's actually an array)
	reppath = ""
	--
	if displaytest == 1
	{
		"* I take all the music files in the folder.\n"
	}
	--
	path = SPLIT(FENUM(folderpath),",")
	foreach path ; file
	{
			reppath = reppath + "," +folderpath +"\"+file
	}
	addfiledropped
}

addfiledropped
{
	//If I add a playlist, Allfilepath already have all the musics so there is nothing to do. 
	if addaplaylist != 1
	{
		AllFilepath = SPLIT (reppath, ",")
	}
	nbfiledropped = ARRAYSIZE (AllFilepath)
	nbmudropped = 0
	nbmudroppedyet = 0
	nbmuinside = 0
	--
	if displaytest == 1
	{
		"* Now I add the songs and want %(AllFilepath[0]) and %(AllFilepath[1]), the first and the second music.\n"
	}
	--
	foreach AllFilepath ; _Filename
	{
		//"I want %(_Filename)"
		nbsong = ARRAYSIZE(songlist)
		--
		extent = TOLOWER ((SPLITPATH (_Filename))[3])
		//Extension
		_i = REPLACE(_Filename,",","CHR(0x2c)")
		_i = REPLACE(_Filename,"!","CHR(0x21)")
		//Verify that the file is a music. It avoids some problems, as adding to the playlist something that aren't a music.
		if (ASEARCH(extent,'.mid','.mp3','.wav','.wma','.ogg')) != -1
		//Extension
			{
			//Si vous n'avez pas de chemin de repertoire,
			//if reppath != 0 {_Filename = reppath + _Filename}
			//Annuaire
			
			
			//S'il s'agit d'un fichier musical, il est juge s'il figure dans la liste (dans le cas contraire -1)
			//We check if the song is already inside the playlist. If not, ASEARCH will return -1.
			if (ASEARCH(_Filename,songlist)) == -1
			//liste de chansons
			{
				//Add songs that aren't in the list
				//Ajouter des chansons qui ne sont pas incluses dans la liste
				if nbsong <=0 
				{
					songlist = _Filename
				}
				else 
				{
					songlist = (songlist,_Filename)
					--
					if displaytest == 1
					{
						"* I ADDED %(_Filename).\n"
					}
					--
					//We add the path of the new music into the playlist
					nbmudropped++
				}
			}
			//Ne faites rien si vous avez deja des chansons comptees
			//If there is musics already inside the playlist
			else
			{
				nbmuinside = 1
			//Nombre de chansons deja dans
			}
		}
	}
	if nbmudropped >= 1 
	{
		//Mettre a jour la liste
		//Disparition du menu d'aide
		--
		if ARRAYSIZE(songlist) == 1 {songlist = songlist[0]}
		nbsong = ARRAYSIZE(songlist)
		// Nombre de chansons == 1 { liste de chansons =  liste de chansons [0]}
		"\0* %(nbmudropped) SONG(s) ADDED! \w5\nTHERE ARE %(nbsong)\nsong(s) IN THE\nplaylist."
		--
		if mode != "CD" 
		{
			//Nombre de chansons ajoutees, mode
			"\n\n[half]\![*]\q[* PLAY MUSIC,muinplaylist]    \![*]\q[CLOSE,□close]"
		}
	}
//Je ne pouvais pas ajouter de chansons
//Can't add some musics to the playlist
	else 
	{
		//Disparition du menu d'aide
		--
		//Une ou plusieurs chansons deja entrees ont ete supprimees
		if nbmuinside >0	{"* THERE WERE SONGS I ALREADY HAVE"}
		//Nombre de chansons deja dans
	}
	//It was the final step of loading a playlist, so playlistload become 0 again. The playlist has been loaded.
	playlistload = 0
	//sauvegarder
	SAVEVAR
	--
	"\e"
}

Select.AfterUpdate
{
	"\0\s[0]\b2"
	--
	"- Levidre:\f[italic,true] "
	--
	"This is the first update. So this update fixes some typos and introduces Mettaton's dashboard. The big version of the dashboard would have animations in the center. The animations are not ready yet though. The dashboard has play-pause, previous, next, playlist and information buttons.\nAnd obviously there is the after update functions as this notice that are made."
	--
	"\f[italic,false]"
	--
	"\x"
	--
	"\e"
	--
	InitializeNewVariables
}

InitializeNewVariables
{}

 //---- OnInstallBegin -------------------------------------------------------
 //OnInstallBegin is called when a file with an included install.txt is dropped on your ghost. This primarily means things like .nar files for other ghosts or .zip files containing balloons or shells for your ghosts. Primarily ghost-related things. Replace as you want.
 
OnInstallBegin
 {
	"\0\s[0]* OOO! \w8IT SEEMS THAT THERE IS A NEW GHOST I NEED TO INSTALL.\e"
 }

 //---- OnInstallComplete -------------------------------------------------------
 //OnInstallComplete runs once the file above has finished installing.
 
OnInstallComplete
 {
	"\0" //this means your main character will be speaking first, therefore the \s[] at the beginning of each line will be defining your main character's pose.
 	--
	if reference0 == "shell" //This is checking to see if what you installed is classified as a shell in the install.txt. Remember the line in install.txt that said type,ghost or type,shell? This is why that line is important, that's what tells the program what it's installing like so.
 	{	
		if reference1 == "Hypothetical Shell that doesn't actually exist" //This is the name of your shell as you defined in descript.txt in the shell folder.
		{
		"\s[0]We have installed Hypothetical Shell.\w8\1\s[10]Truncated for space.\e"
		//Replace if needed.
		}
		elseif reference1 == "Another totally real shell wow" //As in other files, you can use elseif to expand a list like this to include as many shells as you have or think you'll make. See bootend.dic for more details about elseif.
		{
		"\s[0]We have installed another totally real shell.\e"
		//Replace if needed.
		}
		else //For any other shell they may install.
		{
		"\s[0]We've installed a new shell.\w8\1\s[10]It's called  %(reference1).\e"
		//%(reference1) here would be the name of the shell they installed. Replace if needed.
		}
 	}
	elseif reference0 == "ghost" //if the type of the file they're installing is a ghost.
 	{
		"\s[0]We've installed a new Ghost.\w8\1\s[10]It's called %(reference1).\e"
		//Reference1 here would be the name of the ghost. Replace as needed.
 	}
	elseif reference0 == "balloon" //if the type of file is a balloon
 	{
		"\s[0]We've installed a new balloon.\w8\1\s[10]It's called %(reference1).\e"
		//Reference1 here would be the name of the balloon. Replace as needed.
 	}
	elseif reference0 == "plugin" //if the type of file is a plug-in, like a saori of some kind.
 	{
		"\s[0]We've installed a plug-in.\w8\1\s[10]It's called %(reference1).\e"
		//Reference1 here would be the name of the plug-in. Replace as needed.
 	}
}

//---- OnInstallFailure -------------------------------------------------------
//If the file you dropped fails to install for one reason or another.

OnInstallFailure
 {
	"\0\s[0]THE INSTALLATION HAS FAILED.\n\n[half]\w5" //this first bit of dialogue begins the explanation of what happened. You can replace this with an appropriate intro statement.
	--
	if reference0 == "unsupported" //These if statements determine how it happened to finish the piece of dialogue. This checks if the reason it failed is because it was unsupported.
	{
		"* THIS FILE ISN'T SUPPORTED.\e"
	//Replace with whatever dialogue for an unsupported file you may come up with.
	}	
	elseif reference0 == "extraction" //This checks if the file didn't extract properly.
	{
		"* THE FILE COULD NOT BE EXTRACTED PROPERLY.\e"
	//See above but for a failed extraction.
	}
 }
 
 //--OnInstallRefuse
 //If the file you dropped can't be installed with this ghost.
 
OnInstallRefuse
 {
	"\0\s[0]* IT SEEMS I CANT INSTALL THIS.\w8 IT IS POSSIBLE THIS IS FOR %(reference0) INSTEAD?\e"
 }


//**** Uninstallation ******************************************


//--OnVanishSelecting
//When you select the uninstall option in the menu.

OnVanishSelecting
{
	"\0\s[0]* ARE YOU SURE YOU WISH TO UNINSTALL ME?\e"
}

//OnVanishSelected
//When you decide to uninstall them. 

OnVanishSelected
{
	"\0\s[0]* GOODBYE!\w8 IT WAS A GOOD RUN,\w5 WASN'T IT?\w9\w9\e"
}


//--OnVanishCancel
//If you select Uninstall from the menu, but decide not to do it.

OnVanishCancel
{
	"\0\s[0]* THE SHOW CONTINUES ON!\e"
}

//--TalkVanishButtonHold
//I believe this calls if you click and hold the button on either the menu option for uninstalling them, or possibly the option itself. Be careful if you're trying to test this! You'd hate to uninstall all your hard work, right? I'd probably just make sure the dialogue looks right via Script Input and leave it at that.

TalkVanishButtonHold
{
	"\0\s[0]* IT SEEMS %(usernickname) IS UNSURE ABOUT INSTALLING ME.\e"
}


 //---- OnVanished -----------------------------------------
 //OnVanished is a weird little function. Basically, if you uninstall a ghost, it'll automatically go back to the last ghost you were using. They'll then say this line of dialogue about the ghost you just uninstalled. This is very annoying to test as you can imagine! But unlikely to come up super often, I bet. You might as well be thorough and fill this out as well. Make sure to set up both their poses immediately so they both appear when it switches back to them.
 
OnVanished
 {
	"\0\s[0]IT SEEMS %(refference0) HAS BEEN UNINSTALLED. \w8DISSAPOINTING, ISNT IT?\e"
	
	//reference0 here is the name of the other ghost.
}

 //---- OnBIFFBegin ---------------------------------------------------------------
 //When you first tell the ghost to check your email. An intro statement, if you will. Replace with whatever you like.
 
OnBIFFBegin
 {
	"\0\s[0]CHECKING YOUR EMAIL!\e"
 }

 
 //---- OnBIFFComplete ---------------------------------------------------------------
 //After they've succeeded in checking your mail, they'll report back with how many new emails you have. Replace the dialogue with whatever would be character appropriate.
 
OnBIFFComplete
 {
	if reference0 == 0 //If you have no new mail.
 	{
 		"\0\s[0]* NOTHING NEW, IT SEEMS.\e"
	}
	elseif reference0 == 1 //If you have one new email.
 	{
 		"\0\s[0]* THERE IS ONE NEW EMAIL!\e"
	}
	elseif reference0 > 1 && reference0 < 11 //If you have more than one email but less than eleven.
	{
		"\0\s[0]* THERE ARE %(reference0) EMAILS.\e"
		//reference0 here stores the amount of new email it finds.
	}
	elseif reference0 > 100 //If there are over one hundred new emails.
	{
		"\0\s[0]* THERE ARE %(reference0) UNREAD EMAILS.\W8 BETTER GO AND CHECK IT OUT!\e"
	}
 	else //For any other number of emails.
	{
		"\0\s[0]* THERE ARE %(reference0) EMAILS.\e"
 	}
}

 //---- OnBIFFFailure ---------------------------------------------------------------
 //Various error messages that can happen when your ghost tries to connect to an email account. Replace the dialogue as appropriate.
 
OnBIFFFailure
 {
	if reference0 == "timeout" //If the connection times out.
 	{
 		"\0\s[0]* IT SEEMS THE CONNECTION HAS TIMED OUT.\e"
	}
 	elseif reference0 == "kick" //If the account blocks the ghost from accessing it. I think gmail may do this at first to try and protect your account from unapproved applications using it. It gives you the option to change the setting in the warning email they send you about it.
	{
		"\0\s[0]I'VE BEEN KICKED FROM THE ACCOUNT.\e"
 	}
	elseif reference0 == "defect" //If the information put into the POP menu is wrong.
 	{
 		"\0\s[0]THE EMAIL SEEMS TO HAVE NOT BEEN SET UP PROPERLY.\w8IT CAN BE RESET AT THE POP MENU.\![open,configurationdialog]\e"
	}
 	else //if there's some other kind of error.
	{
		"\0\s[0]I COULDNT FIND THE ACCOUNT.\w8 THE ERROR RETURNED WAS '%(reference0)'.\e"
 	}
}


 //**** Network Update *****************************************************************

 
 //---- OnUpdateBegin -------------------------------------------------------------
 
OnUpdateBegin
 {
	"\0\s[0]STARTING THE UPDATING PROCESS!\w8\e"
	"\0\s[0]UPDATING!\w8\e"
 }

 //---- OnUpdateReady ---------------------------------------------------------
 //If the ghost finds new files to update with.
 
OnUpdateReady
 {
	"\0\s[0]THERE ARE %(reference0) NEW FILES!\w8 %(postiveadj)\w8\e"
	
	//reference0 here storing the number of new files. You don't have to use it if you don't want to.
 }


//---- OnUpdateComplete -------------------------------------------------------------
//When the ghost has finished the update process.

OnUpdateComplete
 {
	if reference0 == "none" //if the ghost had nothing to update with
 	{
 		"\0\s[0]THERE SEEMS TO BE NOTHING.\w8 HOW SAD."
		
		"\0\s[0]NOTHING NEW, SADLY."
	}
 	else //if the ghost has updated successfully
	{
		"\0\s[0]UPDATE SUCCESFUL!"
 	}
	Select.AfterUpdate
}

 //---- OnUpdateFailure -------------------------------------------------------------
 //Error messages if the update process fails in some way.
 
OnUpdateFailure
 {
	if reference0 == "timeout" //if the connection times out
 	{
 		"\0\s[0]* THE UPDATE TIMED OUT.\e"
	}
 	elseif reference0 == "md5 miss" //This is the final message displayed when you've run into an md5 error below at some point.
	{
		"\0\s[0]* THE MD5 HASH FILES DIDN'T MATCH.\e"
 	}
	else //for other errors
 	{
 		"\0\s[0]* THE UPDATE COULD NOT BE COMPLETED.\w8\0\s[0]THE ERROR IS '%(reference0)'.\e"
	}
 }


 //---- OnUpdate.OnDownloadBegin ------------------------------------------------
 //This dialogue plays when they begin downloading a new file. WARNING: if you have big plans for your ghost and are going to make a lot of new image files for it (like... i do, cough) at some point, you're going to want to make these messages very brief, if you even have them at all. The reason is because the user will see these messages for EVERY individual file that they have to download, and if you have, say, 50 pna files in the queue, they will see this dialogue 50 times. Not only that, but displaying the dialogue takes some time, so it also extends the download process.
 //If you aren't planning on adding a ton of files at any point, or are only intending to update the .dic files which will be at most a handful of files, then you can go ahead and put a short message in here. But if your ghost is going to grow in leaps and bounds as you work on it, try and keep these messages as brief as possible to make the process that much faster for your user.
 
OnUpdate.OnDownloadBegin
 {
 	if "aitalk" _in_ reference0 //this is checking to see what kind of files are being downloaded. In this case, it's looking for files called aitalk in the download queue. If you want to have a specialized bit of dialogue for each type of file, you can expand this with elseifs and by replacing aitalk with whatever file you have in mind. Keep in mind my warning above though.
 	{
 		"\0\s[0]* THERE ARE MORE DIALOGUES.\e"
 	}
 	else //for files not specified above
 	{

	}
 }

 //---- OnUpdate.OnMD5CompareBegin ---------------------------------------------------
 //This checks the hash value of the files in the directory and the files listed in updates.dau, I believe. It will do this for every file the user downloads, so I again remind you of my warning above! For every file they download, they will have to go through three sets of unique dialogue, and it can get old fast. Not to mention it slows things down a lot. Again, I would recommend keeping this very brief, if not just replacing it with "..." instead.
 
OnUpdate.OnMD5CompareBegin
 {
	
 }

 //---- OnUpdate.OnMD5CompareComplete --------------------------------------------------
 //After they've checked the MD5 value and it matches. See above about the length of this message.
 
OnUpdate.OnMD5CompareComplete
 {

}

 //---- OnUpdate.OnMD5CompareFailure ---------------------------------------------------
 //If the MD5 values don't match, meaning if the file is missing or damaged or incomplete in some way. If you follow my Network Update directions carefully, this shouldn't come up very often, but don't be surprised if you run into it every now and then by forgetting a file here or there.
 
OnUpdate.OnMD5CompareFailure
 {
	
 }

 
 
 //**** Headlines and RSS Feeds *********************************************************
 //This section has to do with the RSS feeds option on your ghost. If you drag and drop a url on them, they'll add it to a list you can access via the right-click menu, and they'll tell you the last set of updates for the site. This works best with RSS feeds I think, but you can experiment with other sites to see how it works.
 //The headlines you see under the tab are in the headline folder under your base SSP install. So it'd be SSP\headlines. You should see the folders for each linked headline in there. I've very rarely ever messed with them or used them. Likewise, your RSS feeds you've added will be under SSP\rss and I don't think that option will appear in the headlines/RSS tab until you add an RSS feed to a ghost. If you want to clear your RSS feeds or headlines, just delete either of those two folders.
 

 //---- OnHeadlinesenseBegin -----------------------------------------------------------
 //This dialogue comes up when you select something in your RSS feed as an intro, like the other On[Thing]Begin things we've seen so far. Replace as you need.
 
OnHeadlinesenseBegin
 {
	"\0\s[0]* YOU HAVE SELECTED %(reference0).\e"
 }

 //---- OnHeadlinesense.OnFind --------------------------------------------------------
 //This actually displays the feed. My set-up for it is pretty simple but gets the job done, it just has the name of the feed, a line space, then a list of links. You shouldn't have to edit any of these and I probably wouldn't honestly.
 
OnHeadlinesense.OnFind
{
	//----  What page You're on
 	if reference2 == "First" || reference2 == "First and Last"
	{
 		//---- First page
		"\0\b2\s[0]%(reference0):"
 	}
	elseif reference2 == "Next"
 	{
		//---- Second page
 		"\0\b2\s[0]%(reference0):"
	}
 	elseif reference2 == "Last"
	{
 		//---- Last page
		"\0\b2\s[0]%(reference0):"
 	}
	--
 	//---- Headlines
	"\n\n%reference3\n"
 	--
	//---- Pages
 	if reference2 == "First" || reference2 == "Next"
	{
 		//---- Bottom buttons
		"\![*]\q[Next,] - \![*]\q[Stop,Headline_Cancel]\e"
 	}
	else
 	{
		//---- Bottom buttons
 		"\![*]\q[Stop,Headline_Cancel]\e"
 	}
	
}


Select.Headline_Cancel
{
	"\0\s[0]* HEADLINE VIEWING COMPLETED.\e"
	//Actually I take that back, you can change this line of dialogue here for when you're done looking at a headline to something else if you want.
}

 //---- OnHeadlinesenseComplete -------------------------------------------------------
 //This is an odd function. As far as I can gather, this is if the headline information gathering was successful, but there's no new information to report. Possibly if none of the feeds have updated? I'm a little fuzzy on this one.
 
OnHeadlinesenseComplete
 {
	"\0\s[0]* NO UPDATE. %(reference0).\e"
	//reference0 here supposedly always stores the value no update? Again this isn't hugely clear from what I can see. Anyway, this dialogue has never come up for me while using a ghost, so perhaps someone else who runs into it can offer more clarity. For now, you can leave it as is, or write up something else in here if you like.
 }

 //---- OnHeadlinesenseFailure ---------------------------------------------------------
 //This one is more clearly for when the ghost can't get the information it needs about the RSS feed in question.
 
OnHeadlinesenseFailure
  {
	"\0\s[0]* THE FEED CAN'T UPDATE.\w8\n\n[half]" //This sets up an exchange between the two characters. The final part of the exchange continues below in the if statements.
 	--
	if reference0 == "can't download" //if they can't download the rss feed for some reason
 	{
		"* IT CAN'T BE DOWNLOADED."
 	}
	elseif reference0 == "can't analyze" //if they can't read the file for some reason
 	{
		"* IT CAN'T BE ANALYSED."
 	}
	else //other errors
 	{
		"* I DON'T KNOW WHAT THE ERROR IS."
 	}
	--
 	"\e" //And here is the final /e, so you don't need to include it in the if statements above. Another one of the very rare cases this occurs.
}


//**** Ghost Creation and Updating ******************************************************
//These next few are related to creating updates2.dau and .nar files for your ghosts. You'll want to at least make a .nar to distribute your ghost, and if you're going to be using Network Update, you'll also want to make updates2.dau as well. It's unlikely that anyone other than you will see these messages, but you should be thorough and replace them anyway.
//Primarily, you will see these messages when you're creating your updates file or updating your ghost.nar file. More on this on the walkthrough page. However, you could potentially drag and drop another ghost's folder onto them, and they'll also create a nar for that as well, though I've never done that myself. I really wouldn't mess around too much with this. Just follow the directions on the walkthrough page and fill out the dialogue here as required.


 //---- OnUpdatedataCreating -----------------------------------------------------------
 //This will begin the creation of the updates2.dau file. Not unlike the other beginning functions you've seen throughout this file. Replace with whatever you like.
 
OnUpdatedataCreating
 {
	"\0\s[0]* CREATING THE updates2.dau FILE.\e"
 }

 //---- OnUpdatedataCreated -------------------------------------------------------------
 //After the updates2.dau file has been successfully created. Replace with whatever.
 
OnUpdatedataCreated
 {
	"\0\s[0]* SUCCESSFULLY CREATED THE updates2.dau.\e"
 }

 //---- OnNarCreating ------------------------------------------------------------------
 //Along the same lines, this is when they begin creating a .nar file for your ghost. .nars are the format most ghosts will be in, something similar to a .zip file, and this is the format you'll be distributing your ghost in most likely. Replace the dialogue with whatever you like.
 
OnNarCreating
 {
	"\0\s[0]* CREATING THE .nar FILE.\e"
 }

 //---- OnNarCreated -------------------------------------------------------------------
 //After the nar file has been successfully created.
 
OnNarCreated
 {
	"\0\s[0]* SUCCESSFULLY CREATED '%(reference0)'.\e"
	//reference0 here will be the filename of the nar you just created. It should drop the nar in the folder where all your ghosts are listed, ssp\ghost\.
 }

 //****　Clock Synchronization　**********************************************************
//**** 미카 (http://homepage2.nifty.com/ko-ki/)의 사전을 참고했습니다．감사＞こ 나무 성
//This set of functions was done by the above Korean coder, I think. I'll leave their comment there untouched. 
//If you right click your ghost and go into the Utilities menu, there's an option called NTP Time Sync. That's what this is for!
 

 //----OnSNTPBegin ------------------------------------------------------------------
 //As with the others you've seen above, this is the beginning bit of dialogue for this process.
 
OnSNTPBegin
 {
	"\0\s[0]* I WILL CHECK THE TIME.\w9I'M CONNECTING TO %(reference0).\e"
	//%(reference0) here is the site they'll be using to check your clock. I wouldn't worry too much about what the site is or where it's defined, it should be accurate. Replace the dialogue with what you need, you don't have to use reference0 if you don't want to.
 }


 //----　OnSNTPCompare----------------------------------------------------------
 //Now the ghost is going to compare the current time of your computer's system to the time of the site they connected to. Most of this you don't have to touch, just replace the dialogue as indicated.
 
OnSNTPCompare
 {
 	"Retrieved Time: %reference1[1]/%reference1[2]/%reference1[0] %reference1[3]:%reference1[4]:%reference1[5]\w2\n/
	Your Time: %reference2[1]/%reference2[2]/%reference2[0] %reference2[3]:%reference2[4]:%reference2[5]\w2\n/
 	\n"
	--
 	if reference3 == 0 //if your clock is already accurate.
	{
 		"\0\s[0]* YOUR CLOCK IS ACCURATE.\e" //replace this with whatever message you like.
	}
 	else //if your clock doesn't match
	{
	//you can redo this top line up to the \n\n[half] part, but leave the \q options alone.
 		"\0\s[0]* YOUR CLOCK DOESN'T MATCH.\w6\nSHOULD I FIX IT?\n\n[half]/
		\![*]\q[Fix it,OnSNTPEXECUTE]\n/
 		\![*]\q[Leave it alone,OnSNTPCANCEL]\e"
	}
 }

 //----　OnSNTPEXECUTE ------------------------------------------------------------------
 //After they've fixed your clock. Note the \6 tag - that's what actually changes the clock. Do not remove that tag. Replace the rest with what you want.
 
OnSNTPEXECUTE
 {
	"\0\s[0]* THE CLOCK HAS BEEN FIXED.\6\e"
 }

 //----　OnSNTPCANCEL ------------------------------------------------------------------
 //If you decide not to change your clock when they tell you it doesn't match. Replace with what you want.
 
OnSNTPCANCEL
 {
	"\0\s[0]* ALRIGHT,\w5 AS YOU WANT.\e"
 }

 //----　OnSNTPFailure ------------------------------------------------------------------
 //If they can't change the clock for some reason, possibly because the site timed out but it could be anything. Replace with what you want.
 
OnSNTPFailure
 {
	"\0\s[0]* I DIDN'T SUCCESS. \w8SOMETHING WENT WRONG.\e"
 }


 //--OnDisplayChange----------------------------------------------------------------
 //This is when you change the resolution of your monitor while your ghost is running. Don't mess with any of this, it should be fine as is.
 
OnDisplayChange
 {
	displaybpp    = reference0
 	displaywidth  = reference1
	displayheight = reference2
 }



 //---- OnScreenSaverStart -------------------------------------------------------
 //This happens if your ghost is running when your screensaver starts up. This and OnScreenSaverEnd are optional, you can delete them both if you like. In fact, you can just delete the rest of the functions from this point on if you're tired and lazy, but we're close to the end, so don't give up yet!
 
OnScreenSaverStart
 {
	"\0\s[0]* THE SCREENSAVER IS STARTING.\w7\e"
	//Replace with what you want.
 }

 //---- OnScreenSaverEnd -------------------------------------------------------
 //When you wiggle your mouse and stop your screensaver.
 
OnScreenSaverEnd
 {
	"\0\s[0]* THE SCREENSAVER IS GONE;.\e"
	//Replace with what you want.
 }


 //---- OnApplicationBoot ---------------------------------------------------------------
 //This supposedly is for when a program starts but I have honestly never gotten it to work or seen it happen. I wouldn't sweat these three related functions too much.
 
OnApplicationBoot
 {
	"\0\s[0]* %(reference0) JUST STARTED.\e"
 }

 //---- OnApplicationClose --------------------------------------------------------------
 //See above, but for the program closing.
 
OnApplicationClose
 {
	"\0\s[0]* %(reference0) HAS CLOSED.\e"
 }

 //---- OnApplicationFileOpen ----------------------------------------------------------
 //I believe this is for when the program detailed above opens a document file of some kind.
 
OnApplicationFileOpen
 {
	"\0\s[0]* OPENING %(reference1).\e"
 }




 //---- OnBatteryLow -----------------------------------------------------------------
 //This is for when your laptop's battery is low. I don't have a laptop, so I've never really been able to test this. Perhaps someone with a laptop can report back to me with more details about the battery related functions?
 
OnBatteryLow
 {
	"\0\s[0]* THE LAPTOP BATTERY IS LOW. \w8\0\s[0]THERE IS %(reference0) POWER LEFT.\e"
	//reference0 here is the amount of battery power. Replace as you like.
 }
 
 //--OnBatteryCritical-----------------------------------------------------------------
 //Likewise, this is for if your battery is REALLY low. Replace as you like.
 
OnBatteryCritical
{
	"\0\s[0]* THE LAPTOP BATTERY IS CRITICALLY LOW.\e"
}
 
//--OnBatteryNotify--------------------------------------------------------------------
//This I believe checks to see if your laptop is plugged in or not. Originally this bit of code was commented out, so I suspect it may not work. It may need a plug-in of some sort. Either way, you can delete it or fill it out as you like.
//Actually investigating this, leaving this in makes it so the ghost updates you about whether or not your computer is plugged-in constantly, which makes no sense for desktops. No doubt why it was commented out. Just leave it be.

/*
OnBatteryNotify
{
	if reference2 == "online" //If your laptop is plugged in
	{
		"\0\s[0]The laptop is plugged in.\e"
	}
	elseif reference2 == "offline" //If you unplug your laptop
	{
		"\1\s[10]The laptop is not plugged in.\w8\0\s[0]Watch your battery.\e"
	}
}
*/


 //---- OnSysResourceLow -----------------------------------------------------------
 //A notification for if your system resources are low. I've never seen this happen and my system resources can run pretty low sometimes (woo windows xp woooo), so I suspect this one also may not work without the right plug-in, but I could be wrong. You might as well fill it out.
 
OnSysResourceLow
 {
	"\0\s[0]* THE SYSTEM RESOURCES ARE LOW.\e"
 }


 //--OnShellScaling
 //This is a function however that you may actually use! Or at least, I often use it when desktop space is at a premium (which it always is on my computer). If you right click the ghost, you can go to Shell Scaling, which lists a number of different percentages. Those are the percentages your ghost can be scaled in size up or down. You can try them out yourself to get an idea of what I mean. I often scale mine down to get them out of the way sometimes. This adds dialogue whenever you change their scale. You don't have to do this if you don't want to, in which case they will just stay silent when you size them up or down. If that's what you want, just delete the whole thing.
 
OnShellScaling
{
	if reference0 >= 150 //this number is the percentage that you're scaling the ghost up to. In this case, it's 150%.
	{
		"\0\s[0]* I'M NOW 150% MY ORIGINAL SIZE.\e"
	}
	elseif reference0 == 100 //this is 100%
	{
		"\0\s[0]* I'M NORMAL SIZE.\e"
	}
	elseif reference0 == 50 //this is 50%. You can add any percentage that's listed under shell scaling if you want using more elseifs.
	{
		"\0\s[0]* I'M NOW 50% MY ORIGINAL SIZE.\e"
	}
}